# Gnuplot script to plot the evolution of the collective variable
# (CaM-W431 Capha-W431 aromatic ring) as a function of time
# and the last five free energy surfaces (along the last 200 ns, 
# one every 50 ns). 

# To run it, execute gnuplot plot_angle_fes.gpl

set term pngcairo enhanced size 50cm,50cm 
set output "metadynamics.png"
set key bottom center font ",18"

set multiplot layout 2,2 

# Angle as a function of time ("one turn") 
set tmargin at screen 0.90; set bmargin at screen 0.60
set label 1 "A" at graph 0.05,0.9 front font ",30"
set title "Angle VS time (one turn)" font ",18" 
set xlabel "Time (ns)" offset 0,-1 font ",18"
set xtics 0,500
set xrange [0:2500]
set ylabel "Angle (rad)" offset -1,0 font ",18"
set yrange [0.0:pi]
set format y "%.1f"
set xtics font ",18"
set ytics font ",18"
# Modify the input file names as needed
plot "/scratch/rafael/CaMRecog/MetadW37Angle_S05_F/Metad/Test/HILLS_FIXED" \
u ($1/1000):2 w l notitle 

# Angle as a function of time ("two turns") 
set label 1 "B"
set title "Angle VS time (two turns)" 
# Modify the input file names as needed
plot "/scratch/rafael/CaMRecog/MetadW37Angle_S05_TwoTurns_F/Metad/Test/HILLS_FIXED" \
u ($1/1000):2 w l notitle 

# FES as a function of time ("one turn") 
set tmargin at screen 0.46; set bmargin at screen 0.16
set label 1 "C" 
set title "Free energy surface VS time (one turn)" 
set xlabel "Angle (rad)"
set xrange [0.0:pi]
set xtics 0,1,3 
set format x "%.1f"
set autoscale y
set ylabel "Free energy (kcal/mol)" 
set format y "%.1f" 
# Modify the input file names as needed
# The last five fes_n files [from fes_(n-6) to fes_(n-1), as fes_n is just
# the final surface, i.e. the same as fes_(n-1)] should be plotted.
plot "/scratch/rafael/CaMRecog/MetadW37Angle_S05_F/Metad/Test/fes_45.dat" \
u 1:($2/4.184) w l lw 2 t "2300 ns", \
     "/scratch/rafael/CaMRecog/MetadW37Angle_S05_F/Metad/Test/fes_46.dat" \
u 1:($2/4.184) w l lw 2 t "2350 ns", \
     "/scratch/rafael/CaMRecog/MetadW37Angle_S05_F/Metad/Test/fes_47.dat" \
u 1:($2/4.184) w l lw 2 t "2400 ns", \
     "/scratch/rafael/CaMRecog/MetadW37Angle_S05_F/Metad/Test/fes_48.dat" \
u 1:($2/4.184) w l lw 2 t "2450 ns", \
     "/scratch/rafael/CaMRecog/MetadW37Angle_S05_F/Metad/Test/fes_49.dat" \
u 1:($2/4.184) w l lw 2 t "2500 ns" 

# FES as a function of time ("two turns") 
set label 1 "D" 
set title "Free energy surface VS time (two turns)" 
# Modify the input file names as needed
# The last five fes_n files [from fes_(n-6) to fes_(n-1), as fes_n is just
# the final surface, i.e. the same as fes_(n-1)] should be plotted.
plot "/scratch/rafael/CaMRecog/MetadW37Angle_S05_TwoTurns_F/Metad/Test/fes_45.dat" \
u 1:($2/4.184) w l lw 2 t "2300 ns", \
     "/scratch/rafael/CaMRecog/MetadW37Angle_S05_TwoTurns_F/Metad/Test/fes_46.dat" \
u 1:($2/4.184) w l lw 2 t "2350 ns", \
     "/scratch/rafael/CaMRecog/MetadW37Angle_S05_TwoTurns_F/Metad/Test/fes_47.dat" \
u 1:($2/4.184) w l lw 2 t "2400 ns", \
     "/scratch/rafael/CaMRecog/MetadW37Angle_S05_TwoTurns_F/Metad/Test/fes_48.dat" \
u 1:($2/4.184) w l lw 2 t "2450 ns", \
     "/scratch/rafael/CaMRecog/MetadW37Angle_S05_TwoTurns_F/Metad/Test/fes_49.dat" \
u 1:($2/4.184) w l lw 2 t "2500 ns" 

unset multiplot 

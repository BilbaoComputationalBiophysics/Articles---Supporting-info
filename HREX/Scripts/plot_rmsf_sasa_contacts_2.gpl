
# Gnuplot script to plot the RMSF and SASA
# of the SK2 residues, and the contacts 
# between W431/L427 and CaM.
# It was used to generate the plot for the
# last two HREX simulations (one turn, outward
# and two turns, inward).
# To run it, execute:
# gnuplot plot_rmsf_sasa_contacts_2.gpl

set term pngcairo enhanced size 50cm,50cm 
set output "sk2CaM_rmsf_sasa_contacts_swappedTRP.png"

set multiplot layout 2,2 font ",35" 
set xrange [421:437]
set key font ",18"
set xtics font ",18"
set ytics font ",18"
set tmargin at screen 0.90; set bmargin at screen 0.60
set lmargin at screen 0.13; set rmargin at screen 0.46 

# RMSF
set label 1 "A" at graph 0.05,0.9 front font ",30"
set label 2 "W431" at first 430,0.55 front font ",25" 
set title "RMSF of SK2 (N421-T437)" font ",18" 
unset xlabel
set ylabel "RMSF (nm)" offset -2,0 font ",18"
set yrange [0:1]
set format y "%.1f"
data = "< (paste /dipc/rafael/CaMRecog/OneTurnTRPOutward/Prod/R0/rmsf_channel.xvg \
/dipc/rafael/CaMRecog/TwoTurnsTRPInward/Prod/R0/rmsf_channel.xvg)"
plot data u ($1+394):2 with lines lw 2 lc rgb "#bc1e0d" t "One turn, W431 out", \
     data u ($3+394):4 with lines lw 2 lc rgb "#4d9208" t "Two turns, W431 in", \
     data u ($1+394):($2-$4) with lines lw 2 lc rgb "black" t "Difference"

# SASA
set lmargin at screen 0.62; set rmargin at screen 0.95 
set label 1 "B"
set label 2 at first 430,1.1  
set title "SASA of SK2 (N421-T437)" 
set yrange [-1.5:2.5]
set ylabel "SASA (nm^2)"
#set key at graph 0.80,0.95 
plot "/dipc/rafael/CaMRecog/OneTurnTRPOutward/Prod/R0/sasaSK2.xvg" \
u ($1+394):2:3 w yerrorbars linestyle 2 lw 2 lc rgb "#bc1e0d" t "One turn, W431 out", \
    "/dipc/rafael/CaMRecog/TwoTurnsTRPInward/Prod/R0/sasaSK2.xvg" \
u ($1+394):2:3 w yerrorbars linestyle 2 lw 2 lc rgb "#4d9208" t "Two turns W431 in", \
    "/dipc/rafael/CaMRecog/TwoTurnsTRPInward/Prod/R0/sasaSK2diff.xvg" \
u ($1+394):2:3 w yerrorbars linestyle 2 lw 2 lc rgb "black" t "Difference" 

# SK2-CaM contacts
#set autoscale fix
set tmargin at screen 0.46; set bmargin at screen 0.16 
set lmargin at screen 0.13; set rmargin at screen 0.46 
set palette defined (0.0 "black", 0.6 "white") 
set cblabel "Distance (nm)" offset screen 0.4,0.024 font ",20" 
set colorbox user size 0.55,0.02 origin 0.15,0.025 horizontal 
set xlabel "SK2 residue" font ",18"
set ylabel "CaM residue"
set format y "%g" 
set xrange [1:2] 
set yrange [81:147]
set format cb "%.1f"
set cbrange [0.0:0.6]
set cbtics font ",18"
set colorbox user size 0.55,0.0125 origin 0.15,0.05 horizontal 

# SK2-CaM contacts, one turn 
set label 1 "C"
unset label 2
set label 3 "M144" at first 1.55,143 front font ",18" 
set label 4 "M124" at first 1.55,124 front font ",18" 

#set label 3 "M145" at first 1.35,144 front font ",18" 
#set label 4 "F141" at first 1.35,140 front font ",18" 
#set label 5 "L112" at first 1.35,113 front font ",18" 
#set label 6 "M109" at first 1.35,109 front font ",18" 
#set label 7 "V91,F92" at first 1.27,92 front font ",18" 
#set label 8 "A88" at first 1.39,88 front font ",18" 
set title "L427,W431-CaM contacts\n(one turn, W431 outward)"
set xtics ("L427" 1.25, "W431" 1.75)
plot "/dipc/rafael/CaMRecog/OneTurnTRPOutward/Prod/R0/contactMap_431-427.dat" \
u ($1):($2+61):3 with image notitle 

# SK2-CaM contacts, two turns 
set lmargin at screen 0.62; set rmargin at screen 0.95 
set label 1 "D"
set label 3 "M145" at first 1.3,144 
unset label 4

#set label 3 "M144,M145" at first 1.55,144 font ",18" 
#set label 4 "M124" at first 1.55,125 font ",18" 
#set label 5 "V121" at first 1.55,120 font ",18" 
#set label 6 "E114,L116" at first 1.55,114 font ",18" 
#set label 7 "M109,L112" at first 1.55,110 font ",18" 
#set label 8 "L105" at first 1.55,105 font ",18" 
#set label 9 "F92" at first 1.55,92 front font ",18" 
set title "L427,W431-CaM contacts\n(two turns, W431 inward)"
plot "/dipc/rafael/CaMRecog/TwoTurnsTRPInward/Prod/R0/contactMap_431-427.dat" \
u ($1):($2+61):3 with image notitle 

set tmargin at screen 0.42; set bmargin at screen 0.26 
set lmargin at screen 0.75; set rmargin at screen 0.88 
unset label 1
set label 3 "M124,E127" at first 1.1,115 front font ",18" 
set label 4 "E120,E123" at first 1.1,106 front font ",18" 
set label 5 "M145,A147" at first 1.1,140 front font ",18" 
unset title
unset xlabel
unset ylabel
set xtics ("V426" 1.25, "W431" 1.75)
plot "/dipc/rafael/CaMRecog/TwoTurnsTRPInward/Prod/R0/contactMapHelical_431-426.dat" \
u ($1):($2+61):3 with image notitle 

unset multiplot 
